;;; Atoms
(def {nil} {})
(def {true} #t)
(def {false} #f)

(def {fun} (\ "(fun args doc body)\nDefines a new function named after the head of args" {f c b} {
  def (head f) (\ c (tail f) b)
}))

(def {fun-no-doc} (\ "(fun-no-doc args body)\nDefines a new function named after the head of args" {f b} {
  def (head f) (\ (tail f) b)
}))

(load-verbose "functional.tin")
(load-verbose "logical.tin")
(load-verbose "numeric.tin")
(load-verbose "cond.tin")

;;; Misc Functions

(fun {flip f a b} "(flip f a b)\nflip the arguments a and b of the given function f" {f b a})
(fun {ghost & xs} "(ghost & xs)\ndrop first argument and evaluate" {eval xs})
(fun {comp f g x} "(comp f g x)\napply f to the result of g applied to x" {f (g x)})

(load-verbose "list.tin")

;;; Other Fun

; Fibonacci
(fun {fib n} "(fib n)\nthe nth number in the fibonacci sequence" {
  select
    { (== n 0) 0 }
    { (== n 1) 1 }
    { otherwise (+ (fib (- n 1)) (fib (- n 2))) }
})

(load-verbose "test.tin")

(describe "sanity tests"
  {it "checks variable equivalence" { do
    (should #t #t)
  }}
  {it "checks assignment" { do
    (= {x} #t)
    (should x #t)
  }}
  {it "checks re-assignment" { do 
    (= {x} #f)
    (= {x} #t)
    (should x #t)
  }}
  {it "checks function calling" { do 
    (should 2 (+ 1 1))
  }}
)

(do 
  (= {fh} (file/open "banner.txt" file/read-mode))
  (println (file/read fh))
  (= {str-version} (map show version))
  (= {started-on} (join "session started on: " (datetime/current)))
  (= {host-info} (join "Your host is running " (sys/os) "/" (sys/arch) " and was compiled with " (sys/compiler) " " (sys/compiler-version)))
  (println "Version:" (join (fst str-version) "." (snd str-version) "." (trd str-version) " \"Nakatomi Plaza\" " started-on))
  (println host-info)
  (file/close fh))
 
